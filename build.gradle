plugins {
    id 'java-library'
    id 'org.dmfs.gver' version '0.18.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

sourceCompatibility = 1.8


gver {
    issueTracker GitHub {
        repo = "dmfs/jdav"
        if (project.hasProperty("GITHUB_API_TOKEN")) {
            accessToken = GITHUB_API_TOKEN
        }
    }
    changes {
        are none when {
            affects only(matches(~/.*\.md/))
        }
        are major when {
            commitMessage contains(~/(?i)#(major|break(ing)?)\b/)
        }
        are minor when {
            commitMessage contains(~/(?i)#(?<issue>\d+)\b/) {
                where("issue") { isIssue { labeled "enhancement" } }
            }
        }
        are patch when {
            commitMessage contains(~/(?i)#(?<issue>\d+)\b/) {
                where("issue") { isIssue { labeled "bug" } }
            }
        }
        are minor when {
            commitMessage contains("#feature\\b")
        }
        otherwise patch
    }
    preReleases {
        on ~/main/ use { "beta" }
        on ~/(.*\/)?(?<name>.*)/ use { "alpha-${group('name')}.1" }
    }
    releaseBranchPattern ~/main$/
}


allprojects {
    group 'org.dmfs'
    repositories {
        mavenCentral()
    }
}

configurations {
    pom
}

if (project.hasProperty('SONATYPE_USERNAME') && project.hasProperty('SONATYPE_PASSWORD')) {
    apply plugin: 'io.github.gradle-nexus.publish-plugin'

    nexusPublishing {
        repositories {
            sonatype {
                username = SONATYPE_USERNAME
                password = SONATYPE_PASSWORD
            }
        }
    }
}

apply from: 'jacoco.gradle'
apply from: 'publish.gradle'

dependencies {
    api 'org.dmfs:rfc5545-datetime:0.3'
    api 'org.dmfs:xmlobjects:0.4.3'
    api 'org.dmfs:http-client-essentials:0.20'
    api 'org.dmfs:http-client-types:0.20'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.saynotobugs:confidence-core:0.15.1'
    testImplementation 'org.dmfs:jems2-testing:2.18.0'
    testImplementation 'org.saynotobugs:confidence-incubator:0.15.1'
}


test {
    useJUnitPlatform()
}
